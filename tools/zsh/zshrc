# Dotfiles location
export DOTFILES="$HOME/_dotfiles"

# Case-insensitive tab completion (cached, regenerates daily)
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'

# Directory stack navigation (1-9 to jump to recent dirs)
setopt AUTO_PUSHD           # Push dirs onto stack automatically
setopt PUSHD_IGNORE_DUPS    # No duplicates in stack
setopt PUSHD_SILENT         # Don't print stack after pushd

# Spelling correction for commands and arguments
setopt CORRECT              # Correct mistyped commands
setopt CORRECT_ALL          # Correct mistyped arguments too
SPROMPT='Correct %F{red}%R%f to %F{green}%r%f? [nyae] '
alias 1='cd -1'
alias 2='cd -2'
alias 3='cd -3'
alias 4='cd -4'
alias 5='cd -5'
alias 6='cd -6'
alias 7='cd -7'
alias 8='cd -8'
alias 9='cd -9'
alias d='dirs -v'           # List recent dirs with numbers

# Job control - select backgrounded processes with fzf
j() {
  local job=$(jobs | fzf --height=40% --reverse | sed 's/\[\([0-9]*\)\].*/\1/')
  [[ -n "$job" ]] && fg %$job
}

# Aliases
alias q=exit
alias c=clear
alias e='$EDITOR'

# zsh-autosuggestions
if [[ -f /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
  source /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh
elif [[ -f /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
  source /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# zsh-syntax-highlighting (must be sourced last)
if [[ -f /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
  source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
elif [[ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
  source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# Adjust terminal theme
themesetting () {
	# Launch in tmux popup if available and not already in one
	if [[ -n "$TMUX" && -z "$THEMESETTING_POPUP" ]]; then
		tmux display-popup -E -w 60% -h 60% "THEMESETTING_POPUP=1 zsh -ic 'themesetting'"
		return
	fi

	local themes_dir="$DOTFILES/themes"
	if [[ ! -d "$themes_dir" ]]
	then
		echo "Error: themes directory not found at $themes_dir"
		echo "Set DOTFILES environment variable to your dotfiles location"
		return 1
	fi

	# Get machine defaults from inventory
	local inventory="$DOTFILES/localhost.yml"
	local default_color=$(grep -A20 "$(hostname)" "$inventory" 2>/dev/null | grep "theme_color:" | head -1 | awk '{print $2}')
	local default_font=$(grep -A20 "$(hostname)" "$inventory" 2>/dev/null | grep "theme_font:" | head -1 | awk '{print $2}')
	local default_style=$(grep -A20 "$(hostname)" "$inventory" 2>/dev/null | grep "theme_style:" | head -1 | awk '{print $2}')
	# Fall back to global defaults
	[[ -z "$default_color" ]] && default_color=$(grep "theme_color:" "$inventory" 2>/dev/null | head -1 | awk '{print $2}')
	[[ -z "$default_font" ]] && default_font=$(grep "theme_font:" "$inventory" 2>/dev/null | head -1 | awk '{print $2}')
	[[ -z "$default_style" ]] && default_style=$(grep "theme_style:" "$inventory" 2>/dev/null | head -1 | awk '{print $2}')

	# Parse available theme choices from shared YAML files
	local choices=$(
    grep -E '^      [a-z_]+:$' "$themes_dir/_color.yml" 2>/dev/null | grep -vE '(args|file|stat|when|loop|shell|register|path|state):' | sed 's/^ *//; s/:$//' | sed 's/^/color: /'
    grep -E '^      [a-z_]+:$' "$themes_dir/_font.yml" 2>/dev/null | grep -vE '(args|file|stat|when|loop|shell|register|path|state):' | sed 's/^ *//; s/:$//' | sed 's/^/font: /'
    grep -E '^      [a-z_]+:  *\{' "$themes_dir/_style.yml" 2>/dev/null | sed 's/:.*//; s/^ *//' | sed 's/^/style: /'
  )
	# Put defaults at top, then sorted choices
	local themes=$(echo "DEFAULTS ($default_color / $default_font / $default_style)"; echo "$choices" | sort)
	local selection=$(echo "$themes" | fzf --height=40% --reverse --prompt="Select theme: ")
	if [[ -z "$selection" ]]
	then
		return 0
	fi

	# Handle defaults selection
	if [[ "$selection" == "DEFAULTS"* ]]; then
		echo "Applying machine defaults: $default_color / $default_font / $default_style"
		ansible-playbook "$themes_dir/apply_defaults.yml" --connection=local --limit "$(hostname -s)"
		echo ""
		echo "Reload: tmux source ~/.tmux.conf"
		return
	fi

	local type=$(echo "$selection" | cut -d: -f1)
	local name=$(echo "$selection" | cut -d: -f2 | xargs)
	case "$type" in
		(color) echo "Applying $name color theme..."
			ansible-playbook "$themes_dir/_color.yml" -i "localhost," --connection=local -e "color=$name"
			echo ""
			echo "Reload: tmux source ~/.tmux.conf" ;;
		(font) echo "Applying $name font..."
			ansible-playbook "$themes_dir/_font.yml" -i "localhost," --connection=local -e "font=$name" ;;
		(style) echo "Applying $name style..."
			ansible-playbook "$themes_dir/_style.yml" -i "localhost," --connection=local -e "style=$name"
			echo ""
			echo "Reload: tmux source ~/.tmux.conf"
	esac
}

# Quick idea capture - syncs encrypted via SOPS
idea() {
	local ideas_file="$HOME/.ideas.md"
	local sops_file="$DOTFILES/ideas.sops.md"

	local age_key="age13vqkx5d70vqhdvlnkm3y5htprafj0x0g6nngcqvn65at2lhs73hs3pgsgl"

	case "$1" in
		sync)
			if [[ ! -f "$ideas_file" ]]; then
				echo "No ideas to sync"
				return 0
			fi
			sops --config /dev/null --age "$age_key" -e "$ideas_file" > "$sops_file"
			cd "$DOTFILES" && git add ideas.sops.md && git commit -m "docs: sync ideas" && git push
			echo "Ideas synced and pushed"
			;;
		pull)
			if [[ -f "$sops_file" ]]; then
				sops -d "$sops_file" > "$ideas_file"
				echo "Ideas pulled from repo"
			else
				echo "No ideas.sops.md found"
			fi
			;;
		list)
			[[ -f "$ideas_file" ]] && cat "$ideas_file" || echo "No ideas yet"
			;;
		edit)
			[[ ! -f "$ideas_file" ]] && touch "$ideas_file"
			$EDITOR "$ideas_file"
			;;
		"")
			echo "Usage: idea <text> | idea sync | idea pull | idea list | idea edit"
			;;
		*)
			echo "- $(date +%Y-%m-%d) $*" >> "$ideas_file"
			echo "Captured: $*"
			;;
	esac
}
