# Case-insensitive tab completion (cached, regenerates daily)
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'

# Directory stack navigation (1-9 to jump to recent dirs)
setopt AUTO_PUSHD           # Push dirs onto stack automatically
setopt PUSHD_IGNORE_DUPS    # No duplicates in stack
setopt PUSHD_SILENT         # Don't print stack after pushd
alias 1='cd -1'
alias 2='cd -2'
alias 3='cd -3'
alias 4='cd -4'
alias 5='cd -5'
alias 6='cd -6'
alias 7='cd -7'
alias 8='cd -8'
alias 9='cd -9'
alias d='dirs -v'           # List recent dirs with numbers

# Job control - select backgrounded processes with fzf
j() {
  local job=$(jobs | fzf --height=40% --reverse | sed 's/\[\([0-9]*\)\].*/\1/')
  [[ -n "$job" ]] && fg %$job
}

# Aliases
alias q=exit
alias c=clear
alias e='$EDITOR'

# zsh-autosuggestions
if [[ -f /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
  source /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh
elif [[ -f /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
  source /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# zsh-syntax-highlighting (must be sourced last)
if [[ -f /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
  source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
elif [[ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
  source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# Adjust terminal theme
themesetting () {
	local themes_dir="$DOTFILES/themes"
	local hostname=$(hostname -s)
	if [[ ! -d "$themes_dir" ]]
	then
		echo "Error: themes directory not found at $themes_dir"
		echo "Set DOTFILES environment variable to your dotfiles location"
		return 1
	fi
	local themes=$(
    ls "$themes_dir"/colors_*.yml 2>/dev/null | xargs -n1 basename | sed 's/colors_/color: /; s/\.yml//'
    ls "$themes_dir"/font_*.yml 2>/dev/null | xargs -n1 basename | sed 's/font_/font: /; s/\.yml//'
  )
	local selection=$(echo "$themes" | sort | fzf --height=40% --reverse --prompt="Select theme: ")
	if [[ -z "$selection" ]]
	then
		return 0
	fi
	local type=$(echo "$selection" | cut -d: -f1)
	local name=$(echo "$selection" | cut -d: -f2 | xargs)
	case "$type" in
		(color) echo "Applying $name color theme..."
			ansible-playbook "$themes_dir/colors_${name}.yml" --connection=local --limit "$hostname"
			echo ""
			echo "Reload: tmux source ~/.tmux.conf" ;;
		(font) echo "Applying $name font..."
			ansible-playbook "$themes_dir/font_${name}.yml" --connection=local --limit "$hostname"
	esac
}
